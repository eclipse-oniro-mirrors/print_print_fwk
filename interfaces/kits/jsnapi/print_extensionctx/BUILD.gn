# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/es2abc_config.gni")
import("//build/ohos.gni")

cflags_cc = []

es2abc_gen_abc("gen_print_extension_context_abc") {
  src_js = rebase_path("print_extension_context.js")
  dst_file = rebase_path(target_out_dir + "/print_extension_context.abc")
  in_puts = [ "print_extension_context.js" ]
  out_puts = [ target_out_dir + "/print_extension_context.abc" ]
  extra_args = [ "--module" ]
}

gen_js_obj("print_extension_context_js") {
  input = "print_extension_context.js"
  output = target_out_dir + "/print_extension_context.o"
}

gen_js_obj("print_extension_context_abc") {
  input = get_label_info(":gen_print_extension_context_abc", "target_out_dir") +
          "/print_extension_context.abc"
  output = target_out_dir + "/print_extension_context_abc.o"
  dep = ":gen_print_extension_context_abc"
}

ohos_shared_library("printextensioncontext_napi") {
  sources = [ "print_extension_context_module.cpp" ]
  cflags_cc += [
    "-fno-exceptions",
    "-fdata-sections",
    "-ffunction-sections",
    "-fno-asynchronous-unwind-tables",
    "-fno-unwind-tables",
    "-flto",
    "-Os",
  ]
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    boundary_sanitize = true
    debug = false
    integer_overflow = true
    ubsan = true
  }

  deps = [
    ":print_extension_context_abc",
    ":print_extension_context_js",
  ]

  external_deps = [ "napi:ace_napi" ]

  if (build_variant == "user") {
    cflags_cc += [ "-DIS_RELEASE_VERSION" ]
  }

  relative_install_dir = "module"
  subsystem_name = "print"
  part_name = "print_fwk"
}
