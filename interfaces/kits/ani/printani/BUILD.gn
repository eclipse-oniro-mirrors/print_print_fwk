# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/print/print_fwk/print.gni") 
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")

config("print_ani_config") {
  visibility = [ "*:*" ]
  include_dirs = [
    "include",
    "${print_utils_path}/include",
    "${print_path}/frameworks/models/print_models/include",
  ]
  
  cflags_cc = [
    "-fno-exceptions",
    "-fdata-sections",
    "-ffunction-sections",
    "-fno-asynchronous-unwind-tables",
    "-fno-unwind-tables",
    "-flto",
    "--save-temps",
    "-Os",
  ]
}

ohos_shared_library("print_ani") {
  configs = [ ":print_ani_config" ]
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    boundary_sanitize = true
    debug = false
    integer_overflow = true
    ubsan = true
  }
  include_dirs = [ "${print_path}/frameworks/innerkitsimpl/print_impl/include" ]
  sources = [
    "src/print_ani.cpp",
    "src/print_entry.cpp",
  ]
  deps = [
    "${print_path}/frameworks/helper/print_helper:print_helper",
    "${print_path}/frameworks/innerkitsimpl/print_impl:print_client",
    "${print_path}/frameworks/models/print_models:print_models",
  ]
  external_deps = [
    "ability_base:base",
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:ability_context_native",
    "ability_runtime:ability_manager",
    "ability_runtime:abilitykit_native",
    "ability_runtime:data_ability_helper",
    "ability_runtime:napi_base_context",
    "ability_runtime:ui_extension",
    "access_token:libaccesstoken_sdk",
    "ace_engine:ace_uicontent",
    "bundle_framework:appexecfwk_core",
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "ipc:ipc_core",
    "napi:ace_napi",
    "runtime_core:ani",
    "samgr:samgr_proxy",
  ]
  subsystem_name = "print"
  part_name = "print_fwk"
  output_extension = "so"
}

ohos_prebuilt_etc("print_abc_etc") {
  source = "$target_out_dir/print.abc"
  module_install_dir = "framework"
  subsystem_name = "print"
  part_name = "print_fwk"
  deps = [ ":print" ]
}

generate_static_abc("print") {
  base_url = "./ets"
  files = [
    "./ets/@ohos.print.ets",
  ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/print.abc"
}

group("anipackage") {
  deps = [
    ":print_abc_etc",
    ":print_ani",
  ]
}
