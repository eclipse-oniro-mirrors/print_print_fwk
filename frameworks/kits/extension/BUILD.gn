# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/print/print_fwk/print.gni")
import("//build/ohos.gni")

cflags_cc = []

config("ability_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "${print_path}/frameworks/innerkitsimpl/print_impl/include",
    "${print_path}/frameworks/kits/extension/include",
    "${print_utils_path}/include",
    "//third_party/node/src",
  ]

  cflags_cc += [
    "-fno-exceptions",
    "-fdata-sections",
    "-ffunction-sections",
    "-fno-asynchronous-unwind-tables",
    "-fno-unwind-tables",
    "-flto",
    "-Os",
  ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  defines = [
    "APP_LOG_TAG = \"Ability\"",
    "LOG_DOMAIN = 0xD002200",
  ]
}

config("ability_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "${print_path}/frameworks/innerkitsimpl/print_impl/include",
    "${print_path}/frameworks/kits/extension/include",
    "//third_party/libuv/include",
    "//third_party/json/include",
  ]
}

ohos_shared_library("print_extension_framework") {
  include_dirs = [
    "${print_path}/frameworks/helper/print_helper/include",
    "${print_path}/frameworks/models/print_models/include",
    "${print_path}/frameworks/innerkitsimpl/print_impl/include",
    "${print_path}/frameworks/kits/extension/include",
    "${print_path}/frameworks/helper/print_ani_helper/include",
    "${print_path}/frameworks/ani_util/include",
  ]

  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    boundary_sanitize = true
    debug = false
    integer_overflow = true
    ubsan = true
  }

  sources = [
    "src/js_print_callback.cpp",
    "src/js_print_extension.cpp",
    "src/js_print_extension_connection.cpp",
    "src/js_print_extension_context.cpp",
    "src/print_extension.cpp",
    "src/print_extension_context.cpp",
    "src/ets_print_extension.cpp",
    "${print_path}/frameworks/ani_util/src/print_base_ani_util.cpp",
    "${print_path}/frameworks/ani_util/src/print_object_ani_util.cpp",
    "${print_path}/frameworks/helper/print_ani_helper/src/ani_print_range_helper.cpp",
    "${print_path}/frameworks/helper/print_ani_helper/src/ani_print_job_helper.cpp",
    "${print_path}/frameworks/helper/print_ani_helper/src/ani_print_page_size_helper.cpp",
    "${print_path}/frameworks/helper/print_ani_helper/src/ani_print_margin_helper.cpp",
    "${print_path}/frameworks/helper/print_ani_helper/src/ani_print_preview_attribute_helper.cpp",
  ]
  configs = [ ":ability_config" ]
  public_configs = [
    ":ability_public_config",
    "${ability_runtime_path}/frameworks/native/ability:ability_context_public_config",
  ]

  deps = [
    "${print_path}/frameworks/helper/print_helper:print_helper",
    "${print_path}/frameworks/innerkitsimpl/print_impl:print_client",
    "${print_path}/frameworks/models/print_models:print_models",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_context_native",
    "ability_runtime:ability_manager",
    "ability_runtime:ability_start_options",
    "ability_runtime:abilitykit_native",
    "ability_runtime:app_context",
    "ability_runtime:extensionkit_native",
    "ability_runtime:napi_common",
    "ability_runtime:runtime",
    "ability_runtime:ani_common",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "ipc:ipc_napi",
    "ipc:ipc_single",
    "ipc:rpc",
    "napi:ace_napi",
    "bundle_framework:bms_ani_common",
    "runtime_core:ani",
    "runtime_core:libarkruntime",
  ]

  if (build_variant == "user") {
    cflags_cc += [ "-DIS_RELEASE_VERSION" ]
  }

  subsystem_name = "print"
  part_name = "print_fwk"
}

ohos_shared_library("print_extension_module") {
  sources = [ "${print_path}/frameworks/kits/extension/src/print_extension_module_loader.cpp" ]

  configs = [ ":ability_config" ]
  public_configs = [ ":ability_public_config" ]

  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    boundary_sanitize = true
    debug = false
    integer_overflow = true
    ubsan = true
  }

  deps = [ ":print_extension_framework" ]

  external_deps = [
    "ability_base:base",
    "ability_base:configuration",
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:abilitykit_native",
    "ability_runtime:runtime",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "ipc:ipc_napi",
    "ipc:ipc_single",
    "napi:ace_napi",
  ]

  if (build_variant == "user") {
    cflags_cc += [ "-DIS_RELEASE_VERSION" ]
  }

  relative_install_dir = "extensionability"
  subsystem_name = "print"
  part_name = "print_fwk"
}
